
     model {

    mu_a0 ~ dnorm(0, 0.01)
    sd_a0 ~ dt(0, pow(5, -2), 1)T(0, )
    sd_a1 ~ dt(0, pow(5, -2), 1)T(0, )
  
    mu_psi ~ dnorm(0, 0.01)
    sd_psi ~ dt(0, pow(5, -2), 1)T(0, )
    
    alpha2 ~ dnorm(0, 0.01)
    
    beta1 ~ dnorm(0, 0.01)
    beta2 ~ dnorm(0, 0.01)
    
    for(t in 1:2){
      mu_a1[t] ~ dnorm(0, 1 / 9) # fixed intercept differing by sex
    }
    
    for(g in 1:n_sites) {
      
      mu_psi_site[g] ~ dnorm(mu_psi, sd_psi)
      
      for(i in 1:M) {
        Sex[g, i] ~ dbern(psi.sex[g])
        Sex2[g, i] <- Sex[g, i] + 1
      }
      
      for(t in 1:2){
        sigma[g, t] <- pow(1 / (2*alpha1[g, t]), 0.5) # sd of half normal - derived parameter
        
        alpha1[g, t] ~ dt(0, pow(5, -2), 1)T(0, )
       # lalpha1[g, t] ~ dnorm(mu_a1[t], 1 / sd_a1 / sd_a1) # random intercept
      #  alpha1[g, t] <- exp(lalpha1[g, t])
      } # t
      
      # prob of individual being in the population (for augmentation since N unknown)
      logit(psi[g]) <- mu_psi_site[g] + beta1 * depth[g] + beta2 * forest[g] 
      psi.sex[g] ~ dunif(0, 1)
      
      alpha0[g] ~ dnorm(mu_a0, sd_a0) 
      
      for(i in 1:M) {
        z[g, i] ~ dbern(psi[g])
        s[g, i] ~ dunif(xlim[g, 1], xlim[g, 2]) ##??
        
        for(j in 1:max_trap[g]) { 
          d[g,i,j] <- abs(s[g, i] - trap_locs[g, j])
          
          for(k in 1:K) {
            for(t in 1:2) {
              logit(p0[g, i, j, k, t]) <- alpha0[g] + (alpha2 * C[i, k, g])  
            } # t
          } # k
        } # j
      } # i    
      
      for(i in 1:M) {
        for (j in 1:max_trap[g]) {
          for (k in 1:K) {
            y[i, j, k, g] ~ dbern(p[g, i, j, k])
            p[g, i, j, k] <- z[g, i] * p0[g, i, j, k, Sex2[g, i]] * exp(- alpha1[g, Sex2[g, i]] * d[g, i,j] * d[g, i,j])
          } # i
        } # j
      } # k
      
      # Derived parameters
      N[g] <- sum(z[g , ])
      density[g] <- sum(z[g , ]) / (xlim[g, 2] - xlim[g, 1]) # divided distances by 100 so calculates turtles per 100 m of canal
      
    } # g
}
     